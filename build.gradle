plugins {
    id 'java-library'
    id 'maven-publish'
    id "com.github.spotbugs" version "4.6.0"
}

group 'gr.james'
version '0.10'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

spotbugs {
    spotbugsTest.enabled = false
    effort = "max"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'Stats'
                description = 'A collection of statistical measures and utilities in Java 8'
                url = 'https://github.com/gstamatelat/stats'
                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'gstamatelat'
                        name = 'Giorgos Stamatelatos'
                        email = 'gstamatelat@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/gstamatelat/stats.git'
                    developerConnection = 'scm:git:git@github.com:gstamatelat/stats.git'
                    url = 'https://github.com/gstamatelat/stats'
                }
            }
        }
    }
    repositories {
        maven {
            url = uri("${buildDir}/publishing-repository")
        }
    }
}

wrapper {
    gradleVersion = '6.5.1'
}

javadoc {
    doLast {
        new File(destinationDir, 'stylesheet.css').append(file('src/javadoc/stylesheet.css').text)
        new File(destinationDir, 'script.js').append(file('src/javadoc/script.js').text)
    }
    options.overview('src/javadoc/overview.html')
}

dependencies {
    implementation 'org.knowm.xchart:xchart:3.8.0'
    testImplementation 'junit:junit:4.12'
}
